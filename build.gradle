import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'checkstyle'

group = 'nl.knaw.huygens.lobsang'
version = '0.1.0'

mainClassName = 'nl.knaw.huygens.lobsang.Server'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

ext {
    dropwizardVersion = '1.3.0'
    junitVersion = '4.+'
    gitTag = getVersionName()
}

checkstyle {
    toolVersion = '8.9+'
}

version = "${gitTag}"

dependencies {

    compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-json-logging', version: dropwizardVersion

    testCompile group: 'junit', name: 'junit', version: junitVersion
}

run() {
    args = ['server', 'config-template.yml']
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC")).format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
    archiveName 'lobsang-full.jar'
}

